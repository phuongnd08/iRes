$(document).delegate "#chef_orders_page", "pagecreate", ->
  orderItemsChannel = <%= OrderItem.channel.to_json %>
  ordersChannel = <%= Order.channel.to_json %>
  pubsubClient = new Faye.Client("<%= ServicesSettings.pubsub.mount_point %>")
  backlog = {}

  formatListItem(li) for li in $('#chef_orders_page ul.order_items li[data-order-item-id]')

  processOrder = (order) ->
    if order.deleted || (order.updated && order.ready)
      $("#chef_orders_page ul.order_items[data-order-id=" + order.order_id + "]").fadeOut('slow', ->
        $(this).remove()
      )
    else if order.created
      $(chefOrderTemplate.gsub(order)).appendTo("#chef_orders_page").listview().fadeOut('fast').fadeIn('slow')
      if (backlog[order.order_id])
        processOrderItem orderItem for orderItem in backlog[order.order_id]

  pubsubClient.subscribe ordersChannel, processOrder

  orderItemLI = (orderItem) ->
    $("#chef_orders_page ul.order_items").find("[data-order-item-id=" + orderItem.order_item_id + "]")

  processOrderItem = (orderItem) ->
    if orderItem.deleted
      orderItemLI(orderItem).remove()
    else
      orderUL = $("#chef_orders_page ul.order_items[data-order-id=" + orderItem.order_id + "]")
      if orderUL.length
        orderItemLIHtml = chefOrderItemTemplate.gsub(orderItem)
        if orderItem.created
          orderUL.append(orderItemLIHtml)
        else if orderItem.updated
          orderItemLI(orderItem).replaceWith(orderItemLIHtml)
        orderUL.listview "refresh"
        formatListItem(orderItemLI(orderItem))
      else
        backlog[orderItem.order_id] ||= []
        backlog[orderItem.order_id].push(orderItem)

  pubsubClient.subscribe orderItemsChannel, processOrderItem
