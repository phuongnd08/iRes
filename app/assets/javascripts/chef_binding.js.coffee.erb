$(document).delegate "#orders_page", "pagecreate", ->
  orderItemsChannel = <%= OrderItem.channel.to_json %>
  ordersChannel = <%= Order.channel.to_json %>
  pubsubClient = null
  backlog = {}

  $(li).formatListItem() for li in $('#orders_page ul.order_items li[data-order-item-id]')

  orderUL = (orderId) ->
    $("#orders_page ul.order_items[data-order-id=" + orderId + "]")
  orderHeaderLI = (orderId) ->
    orderUL(orderId).find('[role=heading]')

  processOrder = (order) ->
    if order.deleted
      orderUL(order.order_id).fadeOutAndRemove()
    else
      if order.created && !order.ready
        $(orderTemplate.gsub(order)).appendTo("#orders_page").listview().trigger('create').blinkAndShow()
        if (backlog[order.order_id])
          processOrderItem orderItem for orderItem in backlog[order.order_id]
      else
        if order.updated
          if order.ready
            orderUL(order.order_id).fadeOutAndRemove()
          else
            if orderUL(order.order_id).exists()
              orderHeaderLI(order.order_id).replaceWith(orderHeaderTemplate.gsub(order))
            else
              $(orderTemplate.gsub(order)).appendTo("#orders_page").listview().blinkAndShow()
              addOrderItem(orderItem) for orderItem in order.order_items
            orderUL(order.order_id).listview('refresh').trigger('create').blinkAndShow()
            $(li).formatListItem() for li in orderUL(order.order_id).find('li[data-order-item-id]')

  orderItemLI = (orderItem) ->
    $("#orders_page ul.order_items").find("[data-order-item-id=" + orderItem.order_item_id + "]")

  addOrderItem = (orderItem) ->
    orderItemLIHtml = orderItemTemplate.gsub(orderItem)
    orderUL(orderItem.order_id).append(orderItemLIHtml)

  processOrderItem = (orderItem) ->
    if orderItem.deleted
      orderItemLI(orderItem).remove()
    else
      ul = orderUL(orderItem.order_id)
      if ul.exists()
        if orderItem.created
          addOrderItem(orderItem)
        else
          if orderItem.updated
            orderItemLIHtml = orderItemTemplate.gsub(orderItem)
            orderItemLI(orderItem).replaceWith(orderItemLIHtml)
        ul.listview "refresh"
        orderItemLI(orderItem).formatListItem()
      else
        backlog[orderItem.order_id] ||= []
        backlog[orderItem.order_id].push(orderItem)

  if window.Faye
    pubsubClient = new Faye.Client(pubsubMountpoint)
    pubsubClient.subscribe ordersChannel, processOrder
    pubsubClient.subscribe orderItemsChannel, processOrderItem
