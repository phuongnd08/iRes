class @Waiter
  @selectors =
    orderedPage: "#ordered"
    counter: "#order_page .counter"
    tableNumber: "#order_table_number"

  @getOrderedList = ->
    $(@selectors.orderedPage).find('ul.items')

  @addItem = (id, name) ->
    hash =
      item_id: id
      item_name: name
      child_index: (new Date).getTime()
    itemLI = $(newOrderItemTemplate.gsub hash)
    itemLI.find('input[id$=item_id]').val(id)
    @getOrderedList().append(itemLI)
    @getOrderedList().listview("refresh")
    @updateCounter()

  @removeItem = (itemLI) ->
    itemLI.hide()
    itemLI.find('input[id$=destroy][type=hidden]').val("1")
    @updateCounter()

  @updateCounter = ->
    total = @getOrderedList().find('li').size()
    deleted = @getOrderedList().find('li input[id$=destroy][type=hidden]').toArray().count((input) -> $(input).val() == "1")
    $(@selectors.counter).text(total-deleted)
