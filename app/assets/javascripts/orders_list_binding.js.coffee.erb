orderItemsChannel = <%= OrderItem.channel.to_json %>
ordersChannel = <%= Order.channel.to_json %>

viewerRole = (page) ->
  $(page).attr("data-viewer-role")

highlightedOrderUL = null

unhighlightOrder = (ul) ->
  $(ul).attr('style', '')

highlightOrderUL = (ul) ->
  unhighlightOrder(highlightedOrderUL) if highlightedOrderUL
  $(ul).attr('style', 'border: 2px solid yellow')
  highlightedOrderUL = ul

$(document).delegate "#orders_page", "pagecreate", ->
  pubsubClient = null
  backlog = {}
  role = viewerRole(this)

  $(li).formatListItem() for li in $('#orders_page ul.order_items li[data-order-item-id]')

  ordersDIV = =>
    $(this).find('#orders')

  orderUL = (orderId) ->
    ordersDIV().find("ul.order_items[data-order-id=" + orderId + "]")

  orderHeaderLI = (orderId) ->
    orderUL(orderId).find('[role=heading]')

  focusedDate = $(this).attr('data-date')
  revenueSpan = $(this).find('.collected_revenue')

  revenue = (newRevenue) ->
    if typeof newRevenue == "number"
      revenueSpan.text(newRevenue.toString())
    else
      parseInt(revenueSpan.text())

  showToast = (order) ->
    if order.paid
      $.toast order.name, I18n.t("states.paid")
    else
      if order.ready && !order.served
        $.toast order.name, I18n.t("states.ready")

  addOrUpdateOrder = (order) ->
    orderId = order.order_id

    if (!orderUL(orderId).exists())
      $(orderTemplate.gsub(order)).appendTo(ordersDIV()).listview().trigger('create').blinkAndShow()
      if order.order_items
        addOrderItem(orderItem) for orderItem in order.order_items
    else
      orderHeaderLI(orderId).replaceWith(orderHeaderTemplate.gsub(order))
      orderUL(orderId).listview('refresh').trigger('create').blinkAndShow()

  processOrder = (order) ->
    if order.created_on == focusedDate
      if order.deleted || !order.shown_to[role]
        orderUL(order.order_id).fadeOutAndRemove()
      else
        showToast(order)
        addOrUpdateOrder(order)
        revenue(revenue() + order.revenue_increment)
        orderUL(order.id).listview('refresh').trigger('create')
        highlightOrderUL orderUL(order)

  orderItemLI = (orderItem) ->
    $("#orders_page ul.order_items").find("[data-order-item-id=" + orderItem.order_item_id + "]")

  addOrderItem = (orderItem) ->
    orderItemLIHtml = orderItemTemplate.gsub(orderItem)
    orderUL(orderItem.order_id).append(orderItemLIHtml)
    orderItemLI(orderItem.order_item_id).formatListItem()

  processOrderItem = (orderItem) ->
    if orderItem.deleted
      orderItemLI(orderItem).remove()
    else
      ul = orderUL(orderItem.order_id)
      if ul.exists()
        if orderItem.created
          addOrderItem(orderItem)
        else
          if orderItem.updated
            orderItemLIHtml = orderItemTemplate.gsub(orderItem)
            orderItemLI(orderItem).replaceWith(orderItemLIHtml)
        ul.listview "refresh"
        orderItemLI(orderItem).formatListItem()
      else
        backlog[orderItem.order_id] ||= []
        backlog[orderItem.order_id].push(orderItem)


  if window.Faye && Faye.Client
    pubSubClient = new Faye.Client(pubsubMountpoint)
    pubSubClient.subscribe ordersChannel, processOrder
    pubSubClient.subscribe orderItemsChannel, processOrderItem
    $(this).on 'pagehide', ->
      pubSubClient.unsubscribe ordersChannel
      pubSubClient.unsubscribe orderItemsChannel
