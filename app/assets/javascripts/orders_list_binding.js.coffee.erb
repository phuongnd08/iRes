orderItemsChannel = <%= OrderItem.channel.to_json %>
ordersChannel = <%= Order.channel.to_json %>

viewerRole = (page) ->
  $(page).attr("data-viewer-role")

highlightedOrderUL = null

unhighlightOrder = (ul) ->
  $(ul).removeAttr('style')

highlightOrderUL = (ul) ->
  unhighlightOrder(highlightedOrderUL) if highlightedOrderUL
  $(ul).attr('style', 'border: 2px solid yellow')
  $(ul).blinkAndShow()
  highlightedOrderUL = ul

$(document).delegate "#orders_page", "pagecreate", ->
  pubsubClient = null
  backlog = {}
  role = viewerRole(this)

  setTimeout (->
    $(li).forceCountSpanColor() for li in $('#orders_page ul.order_items li[data-order-item-id]')
  ), 0

  ordersDIV = =>
    $(this).find('#orders')

  orderUL = (orderId) ->
    ordersDIV().find("ul.order_items[data-order-id=" + orderId + "]")

  orderHeaderLI = (orderId) ->
    orderUL(orderId).find('[role=heading]')

  focusedDate = $(this).attr('data-date')
  revenueSpan = $(this).find('.collected_revenue')

  revenue = (newRevenue) ->
    if typeof newRevenue == "number"
      revenueSpan.text(newRevenue.toString())
    else
      parseInt(revenueSpan.text())

  showToast = (order) ->
    if order.paid
      $.toast order.name, I18n.t("states.paid")
    else if order.served
      $.toast order.name, I18n.t("states.served")
    else if order.ready
      $.toast order.name, I18n.t("states.ready")

  playSound = (order) ->
    if order.paid
      Player.play('paid')
    else if order.served
      Player.play('served')
    else if order.ready
      Player.play('ready')
    else
      Player.play('new')

  renderOrderUL = (orderId) =>
    orderUL(orderId).listview('refresh').trigger('create')
    orderUL(orderId).find('li[data-order-item-id]').forceCountSpanColor()

  addOrUpdateOrder = (order) ->
    orderId = order.order_id

    if (!orderUL(orderId).exists())
      $(orderTemplate.gsub(order)).appendTo(ordersDIV()).listview().trigger('create').blinkAndShow()
      if order.order_items
        addOrUpdateOrderItem(orderItem) for orderItem in order.order_items
    else
      orderHeaderLI(orderId).replaceWith(orderHeaderTemplate.gsub(order))
      renderOrderUL(orderId)
    highlightOrderUL orderUL(order)

  processOrder = (order) ->
    if order.created_on == focusedDate
      playSound(order)
      if order.deleted || !order.shown_to[role]
        orderUL(order.order_id).fadeOutAndRemove()
      else
        showToast(order)
        addOrUpdateOrder(order)
        revenue(revenue() + order.revenue_increment)

  orderItemLI = (orderItemId) ->
    $("#orders_page ul.order_items").find("[data-order-item-id='#{orderItemId}']")

  addOrUpdateOrderItem = (orderItem) ->
    if !orderItemLI(orderItem.order_item_id).exists()
      orderItemLIHtml = orderItemTemplate.gsub(orderItem)
      orderUL(orderItem.order_id).append(orderItemLIHtml)
    else
      orderItemLIHtml = orderItemTemplate.gsub(orderItem)
      orderItemLI(orderItem.order_item_id).replaceWith(orderItemLIHtml)
    renderOrderUL(orderItem.order_id)

  processOrderItem = (orderItem) ->
    if orderItem.deleted
      orderItemLI(orderItem.order_item_id).remove()
    else
      if orderUL(orderItem.order_id).exists()
        addOrUpdateOrderItem(orderItem)
      else
        backlog[orderItem.order_id] ||= []
        backlog[orderItem.order_id].push(orderItem)

  if window.Faye && Faye.Client
    pubSubClient = new Faye.Client(pubsubMountpoint)
    pubSubClient.subscribe ordersChannel, processOrder
    pubSubClient.subscribe orderItemsChannel, processOrderItem
    $(this).on 'pagehide', ->
      pubSubClient.unsubscribe ordersChannel
      pubSubClient.unsubscribe orderItemsChannel
